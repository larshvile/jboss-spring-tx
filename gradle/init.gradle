// symlink to $HOME/.gradle/init.gradle or invoke 'gradle --init-script $checkoutPath/gradle/init.gradle'

/*
 * Repositories
 */
beforeProject { p ->
    p.repositories {
        mavenLocal()
        // mavenRepo name: "jboss-releases", url: "https://repository.jboss.org/nexus/content/repositories/releases"
        mavenCentral()
    }
}

/*
 * Use the good old 'target' directory for build output.
 */
beforeProject { p -> p.buildDir = new File(p.buildDir.parentFile, 'target') }

/*
 * Locate this file's parent directory, 'gradle'
 */
final File scriptFolder = buildscript.sourceFile.canonicalFile.parentFile

/*
 * Conventions for loading shared scripts without too much clutter.
 */
class HulteScriptConvention {
  final def project
  final File scriptFolder;

  HulteScriptConvention(def project, File scriptFolder) {
    this.project = project
    this.scriptFolder = scriptFolder
  }

  def applyHulteScript(String script) {
    applyHulteScript(script, project)
  }

  def applyHulteScript(String script, def project) {
    project.apply from: new File(scriptFolder, script + ".gradle")
  }
}

beforeProject { p ->
    p.convention.plugins.hulteScript = new HulteScriptConvention(p, scriptFolder);
}

