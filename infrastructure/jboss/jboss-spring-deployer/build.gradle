/*
 * Creates an installer for snowdrop, the Spring deployer.
 */
apply plugin: 'base'
applyHulteScript "versions"

defaultTasks 'clean', 'uploadArchives'

snowdropVersion = '2.0.1.Final'

dependencies {
    archives "org.jboss.snowdrop:snowdrop-subsystem-as7:$snowdropVersion@zip"
}


staging = file("$buildDir/staging")
distFile = new File(buildDir, "snowdrop-subsystem-as7-${snowdropVersion}.zip")

task createStagingDir(type: Directory) {
    dir = staging
}


task downloadDist(type: Copy) {
   from configurations.archives
   into staging.parentFile
}


task explodeDist(type: Copy, dependsOn: [createStagingDir, downloadDist]) {
    from zipTree(distFile)
    into new File(staging, "modules")
}


task updateModuleDescriptor(type: Copy, dependsOn: explodeDist) {
    from zipTree(distFile)
    include "**/module.xml"
    into new File(staging, "modules")

    // use our main spring module instead of snowdrop specific stuff
    filter { l ->
        l.contains('slot="snowdrop"') ? '        <module name="org.springframework">' : l;
    }
}


task addInitScript(type: Copy, dependsOn: explodeDist) {
    from file('init-spring-deployer.cli')
    into new File(staging, "hulte-scripts")
}


task dist(type: Tar, dependsOn: [updateModuleDescriptor, addInitScript]) {
    from staging
    compression = Compression.GZIP
}


artifacts {
    archives dist
}

assemble.dependsOn dist
uploadArchives.dependsOn assemble

