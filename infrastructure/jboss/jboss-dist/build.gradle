/*
 * Creates an installer for the JBoss distribution.
 */
apply plugin: 'base'
applyHulteScript "versions"


dependencies {
    archives "org.jboss.as:jboss-as-dist:$jbossVersion@tar.gz"
}


def staging = file("$buildDir/staging")


task downloadDist(type: Copy) {
   from configurations.archives
   into staging.parentFile
}

task explodeDist(type: Copy, dependsOn: [downloadDist]) {
    from tarTree(resources.gzip(new File(buildDir, "jboss-as-dist-${jbossVersion}.tar.gz")))
    into staging.parentFile
    doLast {
        assert new File(buildDir, "jboss-as-$jbossVersion").renameTo(staging)
    }
}

task trimDist(dependsOn: explodeDist) << {

    // remove unnecessary top-level folders
    ['appclient', 'docs', 'domain', 'bundles'].each {
        delete file("${buildDir.name}/staging/$it")
    }

    // remove unsupported/unnecessary modules
    ['org/hibernate/envers'].each {
        delete file("${buildDir.name}/staging/modules/$it")
    }
}

task copyConfiguration(type: Copy, dependsOn: trimDist) {
    from file('standalone.xml')
    into file("${buildDir.name}/staging/standalone/configuration")
}


assemble.dependsOn 'dist'
task dist(type: Tar, dependsOn: copyConfiguration) {
    from staging
    compression = Compression.GZIP
}


artifacts {
    archives dist
}

uploadArchives.dependsOn assemble

